plugins {
    id "application"
    id "net.ltgt.errorprone" version "4.3.0"
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation libs.junit.jupiter
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"

    compileOnly "org.jspecify:jspecify:1.0.0"
    testCompileOnly "org.jspecify:jspecify:1.0.0"
    errorprone "com.google.errorprone:error_prone_core:2.45.0"
    errorprone "com.uber.nullaway:nullaway:0.12.15"

    implementation "org.jspecify:jspecify:1.0.0"

    implementation platform("com.fasterxml.jackson:jackson-bom:2.20.1")
    implementation "com.fasterxml.jackson.core:jackson-databind"
    implementation "com.fasterxml.jackson.core:jackson-core"
    implementation "com.fasterxml.jackson.core:jackson-annotations"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

application {
    mainClass = "jp.akimateras.jackson.App"
}

tasks.named("test") {
    useJUnitPlatform()
}

tasks.withType(JavaCompile).configureEach {
    options.errorprone {
        check "NullAway", net.ltgt.gradle.errorprone.CheckSeverity.ERROR
        option "NullAway:AnnotatedPackages", "jp.akimateras.jackson"
        option "NullAway:RecognizedAnnotations", "org.jspecify.annotations.Nullable"
    }
    options.compilerArgs += [
        "-Xlint:all",
        "-parameters"
    ]
}
